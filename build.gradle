buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'groovy'
    id 'idea'
    id 'org.springframework.boot' version '2.0.6.RELEASE'
    id 'nebula.release' version '6.1.1'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'com.gorylenko.gradle-git-properties' version '1.4.17'
    id "org.asciidoctor.convert" version "1.5.3"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

bootRun {
    systemProperties = System.properties
    jvmArgs += "-Dspring.output.ansi.enabled=ALWAYS"
}

bootJar {
    baseName = project.name
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into '/static/docs'
    }
    manifest {
        attributes(
                'Main-Class': 'com.bsb.fms.bsbfmsmockservice.Application'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

group = 'com.bsb.fms'
description = 'COFS Mock Service'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext { appName = 'bsb-fms-mock-service' }

repositories {
    maven { url "$artifactory_contexturl/$artifactory_repo" }
}

ext {
    buildTime = new Date().format("yyyyMMddHHmm")
    springCloudVersion = 'Edgware.SR3'
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    compile('org.codehaus.groovy:groovy-all')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('com.ryantenney.metrics:metrics-spring:3.1.3')
    compile('org.apache.httpcomponents:httpclient')
    compile group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '2.19.0'

    // logging and tracing standardization to SumoLogic
    compile 'com.github.vital-software:sumo-logback:61f68e2049d1b02b61fdd497d67268b1382eb319'
    compile 'com.sumologic.plugins.log4j:sumologic-log4j2-appender:1.0'
    compile 'ch.qos.logback:logback-classic:1.2.3' // purposely upgrade logback from spring base version
    compile 'ch.qos.logback:logback-core:1.2.3'    // purposely upgrade logback from spring base version

    compile group: 'com.datadoghq', name: 'java-dogstatsd-client', version: '2.3'

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile("org.spockframework:spock-spring:1.0-groovy-2.4")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

springBoot {
    buildInfo()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

processResources {
    logger.lifecycle("project.properties about to be expanded")
    logger.lifecycle("----")
    def map = project.properties.sort { it.key }

    if (logger.isEnabled(LogLevel.DEBUG)) {
        map.each { k, v -> logger.debug("${k}:${v}") }
    }

    filesMatching('**/application-actuatorsupport.properties') {
        expand project.properties
    }
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = 'CST'
}

asciidoctor {
    dependsOn test
    sourceDir = file('src/docs')
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    attributes 'snippets': snippetsDir
}

test {
    systemProperty "spring.profiles.active", System.getProperty('spring.profiles.active')
    outputs.dir snippetsDir
}

apply from: 'tests.gradle'
build.dependsOn asciidoctor